import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'dev.architectury.loom' version '1.4-SNAPSHOT'

    // This dependency is only used to determine the state of the Git working tree so that build artifacts can be
    // more easily identified. TODO: Lazily load GrGit via a service only when builds are performed.
    id 'org.ajoberstar.grgit' version '5.2.0'

	id "me.modmuss50.mod-publish-plugin" version "0.3.4"

    id "maven-publish"
}

archivesBaseName = project.archives_base_name
version = "${project.mod_version}${getVersionMetadata()}+mc${project.minecraft_version}"
group = project.maven_group

apply from: "${rootProject.projectDir}/gradle/forge.gradle"
apply from: "${rootProject.projectDir}/gradle/java.gradle"

configurations {
    modIncludeImplementation

    include.extendsFrom modIncludeImplementation
    modImplementation.extendsFrom modIncludeImplementation
}

sourceSets {
    api {
        java {
            compileClasspath += main.compileClasspath
        }
    }

    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }

    compat {
        java {
            compileClasspath += main.compileClasspath
            compileClasspath += main.output
        }
    }
}

loom {
    runs {
        client {
            mods {
                embeddium {
                    sourceSet sourceSets.main
                    sourceSet sourceSets.api
                }
            }
        }
    }
    createRemapConfigurations(sourceSets.compat)
}

tasks.register('apiJar', Jar) {
    archiveClassifier.set "api-dev"

    from sourceSets.api.output
}

tasks.register('remapApiJar', RemapJarTask) {
    dependsOn apiJar
    archiveClassifier.set "api"

    input = apiJar.archiveFile.get().asFile
    addNestedDependencies = false
}

build.dependsOn apiJar
build.dependsOn remapApiJar

jar {
    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir
    from sourceSets.compat.output.classesDirs
    from sourceSets.compat.output.resourcesDir
}

java {
    withSourcesJar()
}

def extraModsDir = "extra-mods-${project.minecraft_version}"

repositories {
    maven { url "https://maven.neoforged.net" }
    exclusiveContent {
        forRepository {
            flatDir {
                name "extra-mods"
                dir file(extraModsDir)
            }
        }
        filter {
            includeGroup "extra-mods"
        }
    }
    maven {
        // curseforge
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // Forge API
    neoForge "net.neoforged:neoforge:${forge_version}"

    // Mods
    modCompatCompileOnly "curse.maven:codechickenlib-242818:${codechicken_fileid}"
    modCompatCompileOnly "curse.maven:immersiveengineering-231951:${ie_fileid}"


    // runtime remapping at home
    for (extraModJar in fileTree(dir: extraModsDir, include: '*.jar')) {
        def basename = extraModJar.name.substring(0, extraModJar.name.length() - ".jar".length())
        def versionSep = basename.lastIndexOf('-')
        assert versionSep != -1
        def artifactId = basename.substring(0, versionSep)
        def version = basename.substring(versionSep + 1)
        modRuntimeOnly("extra-mods:$artifactId:$version")
    }
}

def copyJarNameConsistent = tasks.register('copyJarNameConsistent', Copy) {
    from remapJar // shortcut for createJar.outputs.files
    into project.file("build/libs")
    rename { name -> "embeddium-latest.jar" }
}

def copyJarToBin = tasks.register('copyJarToBin', Copy) {
    from remapJar // shortcut for createJar.outputs.files
    into rootProject.file("bin")
    mustRunAfter "copyJarNameConsistent"
}

tasks.named("remapApiJar") {
    mustRunAfter "copyJarNameConsistent"
}

tasks.named("remapSourcesJar") {
    mustRunAfter "copyJarNameConsistent"
}

tasks.build.dependsOn(copyJarToBin, copyJarNameConsistent)

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(remapSourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

	repositories {
		maven {
			url "file://${System.getenv("local_maven")}"
		}
	}
}

publishMods {
	file = remapJar.archiveFile
	changelog = "https://github.com/embeddedt/embeddium/wiki/Changelog"
	type = STABLE
    modLoaders.add("neoforge")

	curseforge {
		projectId = "908741"
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		minecraftVersions.add(project.minecraft_version)

		incompatible {
			slug = "rubidium"
		}
	}
	modrinth {
		projectId = "sk9rgfiA"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersions.add(project.minecraft_version)

		incompatible {
			slug = "rubidium"
		}
	}

	displayName = "[NEO-${project.minecraft_version}] Embeddium ${project.mod_version}"
}

def getVersionMetadata() {
	// CI builds only
	if (project.hasProperty("build.release")) {
		return "" // no tag whatsoever
	}

	if (grgit != null) {
		def head = grgit.head()
		def id = head.abbreviatedId

		// Flag the build if the build tree is not clean
		if (!grgit.status().clean) {
			id += ".dirty"
		}

		return "-git.${id}"
	}

	// No tracking information could be found about the build
	return "-unknown"
}
